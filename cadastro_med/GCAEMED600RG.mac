GCAEMED600RG	; ?? 04/2024  - SEM TÍTULO - REGRAS
	;
	#include %CSUTICSP
	;
	; Excluir Global Trabalho
	;
	; Excluir global de execução da grid
ExcluirGlobalTrabalho(codTerminal)	;
	$$$VAR
	;
	kill ^mtempGCAEMED(codTerminal)
	;
	quit $$$OK
	;
	; Gerar Global Trabalho
	;
GerarGlobalTrabalho(codEmpresa,codTerminal,tabCpf)	;
	$$$VAR
	new sc,codCPF,mtempGCAEMED,codFicha,codPaciente,cpfEdt
	;
	; # Excluir mtemp
	;
	do $$$KillG(^mtempGCAEMED(codTerminal))
	;
	; # Leitura na global
	;
	set (codCPF,codFicha)=""
	for  set codCPF=$order(^GCAEPAC(codEmpresa,codCPF)) quit:codCPF=""  do
	. ;
	. if $data(tabCpf),'$data(tabCpf(codCPF)) quit
	. ;
	. set sc=$$CG^%CSUTICSP(codCPF,.cpfEdt)
	. ;
	. set $piece(mtempGCAEMED,z,1)=cpfEdt
	. set $piece(mtempGCAEMED,z,2)=$get(^GCAEPAC(codEmpresa,codCPF))	;Pegar código do paciente
	. set codPaciente=$get(^GCAEPAC(codEmpresa,codCPF))
	. set $piece(mtempGCAEMED,z,3)=$piece($get(^GCAEMED(codEmpresa,1,codPaciente)),z,1)	;Pegar Nome do Paciente
	. ;
	. for  set codFicha=$order(^GCAEMED(codEmpresa,8,codCPF,codFicha)) quit:codFicha=""  do
	. . set $piece(mtempGCAEMED,z,4)=codFicha
	. . set ^mtempGCAEMED(codTerminal,codCPF,codFicha)=mtempGCAEMED
	;
	;
	quit $$$OK
	;
	; Gravar Grid
	;
GravarGrid(codEmpresa,codTerminal,rotina,tabCpf,codGrid,codRegistro)	;
	$$$VAR
	new codCPF,codFicha,sc
	;
	; # Obter codRegistro
	set codRegistro=$get(codRegistro)
	;
	; # Leitura na mtemp
	set (codCPF,codFicha)=""
	for  set codCPF=$order(^mtempGCAEMED(codTerminal,codCPF)) quit:codCPF=""  do
	. for  set codFicha=$order(^mtempGCAEMED(codTerminal,codCPF,codFicha)) quit:codFicha=""  do
	. . ;
	. . set sc=$$AtualizarLinhaGrid(codEmpresa,codTerminal,rotina,codCPF,codFicha,codGrid,codRegistro)
	;
	quit $$$OK
	;
	; Atualizar Linha Grid
	;
AtualizarLinhaGrid(codEmpresa,codTerminal,rotina,codCPF,codFicha,codGrid,codRegistro)	;
	$$$VAR
	new corFonte,corFundo,dados,detalha,display,mtempGCAEMED,negrito,sc
	;detalha
	; # Inicializar variáveis
	set (dados,display,detalha,corFundo,corFonte,negrito)=""
	;
	; # Obter dados da mtemp
	set mtempGCAEMED=$get(^mtempGCAEMED(codTerminal,codCPF,codFicha))
	;
	; # Dados
	set $piece(dados,z,1)=$piece(mtempGCAEMED,z,1)
	set $piece(dados,z,2)=$piece(mtempGCAEMED,z,2)
	set $piece(dados,z,3)=$piece(mtempGCAEMED,z,3)
	set $piece(dados,z,4)=$piece(mtempGCAEMED,z,4)
	;
	; # Display
	set display=dados
	;
	; # Detalha
	set detalha=dados
	set $piece(detalha,z,1)=$translate($piece($get(mtempGCAEMED),z,1),".-","")
	;
	set sc=$$GravarLinhas^%CSW1GRID(codTerminal,rotina,codGrid,dados,display,detalha,codRegistro,corFundo,corFonte,negrito)
	;
	quit $$$OK
	;
	; Gerar F7 de CPF
	;
GerarF7CPF(codEmpresa,codTerminal,tabCpf)	;
	$$$VAR
	new codCPF,codPaciente,gcaemed1,mtempF7CPF
	;
	kill ^mtempF7CPF(codEmpresa,codTerminal)
	;
	set (codCPF,mtempF7CPF)=""
	for  set codCPF=$order(^GCAEPAC(1,codCPF)) quit:codCPF=""  do
	. ;
	. set codPaciente=$piece($get(^GCAEPAC(1,codCPF)),z,1)
	. set gcaemed1=$get(^GCAEMED(codEmpresa,1,codPaciente))
	. set $piece(mtempF7CPF,z,1)=codPaciente	;pegar código do paciente
	. set $piece(mtempF7CPF,z,2)=$piece(gcaemed1,z,1) ;pegar nome
	. set ^mtempF7CPF(codEmpresa,codTerminal,codCPF)=mtempF7CPF
	;
	quit $$$OK
	;
	; Gerar F7 de CPF Selecionados
	;
GerarF7CPFSEL(codEmpresa,codTerminal,tabCpf)	;
	$$$VAR
	new codCPF,codPaciente,gcaemed1,mtempF7CPF
	;
	kill ^mtempF7CPFSEL(codEmpresa,codTerminal)
	;
	set (codCPF,mtempF7CPF)=""
	for  set codCPF=$order(^GCAEPAC(1,codCPF)) quit:codCPF=""  do
	. ;
	. if '$data(tabCpf(codCPF)) quit
	. ;
	. set codPaciente=$piece($get(^GCAEPAC(1,codCPF)),z,1)
	. set gcaemed1=$get(^GCAEMED(codEmpresa,1,codPaciente))
	. set $piece(mtempF7CPF,z,1)=codPaciente	;pegar código do paciente
	. set $piece(mtempF7CPF,z,2)=$piece(gcaemed1,z,1) ;pegar nome
	. set ^mtempF7CPFSEL(codEmpresa,codTerminal,codCPF)=mtempF7CPF
	;
	quit $$$OK
	;
ImprimirRelatorio(codEmpresa,codTerminal,cpfCliente,codCliente,codFicha)	;
	$$$VAR
	new caminhoArquivo,mensagem,numeroPagina
	new caminhoTmp,sc
	;
	set mensagem="Período: "
	set numeroPagina=0
	;
	set caminhoTmp=$$caminhoTmp^%CSUTICI()
	set caminhoArquivo=caminhoTmp_cpfCliente_"_fichar_"_codFicha_".pdf"
	;
	set sc=$$iniPDF^%CSW1PDF(caminhoArquivo)
	if $$$ISERR(sc) quit sc
	;
	set sc=$$configPDF^%CSW1PDF("Ficha"_codFicha,"Nome Denvolvedor","Título")
	if $$$ISERR(sc) quit sc
	;
	do Ini(codEmpresa,codTerminal,mensagem,cpfCliente,codCliente,codFicha,numeroPagina)
	;
	set sc=$$finalizaPDF^%CSW1PDF()
	if $$$ISERR(sc) quit sc
	;
	do AbrePagArq^%CSW1PDF(caminhoArquivo)
	;
	quit $$$OK
	;
	; Ini PDF
	;
Ini(codEmpresa,codTerminal,mensagem,cpfCliente,codCliente,codFicha,numeroPagina)	;
	$$$VAR
	new sc,gcaemed8,idade,sexoDoPaciente,cpdEditado,telefoneEditado,convenioEditado,linha
	new codAlimentacao,codMedicamento,codPatologia,codRefeicao,desAlimentacao,desMedicamento
	new desPatologia,desRefeicao,gcaemed1,proxLinha,tabPDF
	;
	kill tabPDF
	set tabPDF="cellpadding:1;font-family:Helvetica;font-size:14;borderlarg:0.4;border:0,0,0,0;"
	;
	do IniCab(.proxLinha,numeroPagina)
	;
	set gcaemed8=$get(^GCAEMED(codEmpresa,8,cpfCliente,codFicha))
	set gcaemed1=$get(^GCAEMED(codEmpresa,1,codCliente))
	;
	; # Nova linha
	;
	set linha=$increment(linha)
	set sc=$$CG^%CSUTICSP(cpfCliente,.cpdEditado)
	set tabPDF(1,1)=$listbuild("CPF:","width:42;height:5;background:#c0c0c0")
	set tabPDF(1,2)=$listbuild(cpdEditado,"width:168;height:5")
	;
	set tabPDF(1,3)=$listbuild("Código:","width:64;height:5;background:#c0c0c0")
	set tabPDF(1,4)=$listbuild(codCliente,"width:282;height:5;")
	;
	set tabPDF(2,1)=$listbuild("Nome:","width:50;height:10;background:#c0c0c0")
	set tabPDF(2,2)=$listbuild($piece(gcaemed1,z,1),"width:506;height:10;")
	;
	set idade=$fnumber(((+$$$horolog-$piece(gcaemed1,Z,2))/365),"",0)
	set tabPDF(3,1)=$listbuild("Idade:","width:48;height:10;background:#c0c0c0")
	set tabPDF(3,2)=$listbuild(idade,"width:508;height:10;")
	;
	set sc=$$ObterSexoPaciente^GCAEMED100RG(gcaemed1,.sexoDoPaciente)
	set tabPDF(4,1)=$listbuild("Sexo:","width:48;height:10;background:#c0c0c0")
	set tabPDF(4,2)=$listbuild(sexoDoPaciente,"width:508;height:10;")
	;
	if $length($piece(gcaemed1,Z,5))=10 do 
	. set telefoneEditado="("_$extract($piece(gcaemed1,Z,5),0,2)_") "_$extract($piece(gcaemed1,Z,5),3,6)_" "_$extract($piece(gcaemed1,Z,5),7,10)
	else  set telefoneEditado="("_$extract($piece(gcaemed1,Z,5),0,2)_") "_$extract($piece(gcaemed1,Z,5),3,7)_" "_$extract($piece(gcaemed1,Z,5),8,11)
	;
	set tabPDF(5,1)=$listbuild("Telefone:","width:68;height:10;background:#c0c0c0")
	set tabPDF(5,2)=$listbuild(telefoneEditado,"width:168;height:10;")
	;
	set sc=$$ObterConvenio^GCAEMED100RG(codEmpresa,$piece(gcaemed1,Z,6),.convenioEditado)
	set tabPDF(6,1)=$listbuild("Convenio:","width:72;height:10;background:#c0c0c0")
	set tabPDF(6,2)=$listbuild(convenioEditado,"width:168;height:10;")
	;
	set tabPDF(7,1)=$listbuild("Email:","width:48;height:10;background:#c0c0c0")
	set tabPDF(7,2)=$listbuild($piece(gcaemed1,Z,7),"width:168;height:10;")
	;
	; # Informações da consulta
	;
	set tabPDF(1,5)=$listbuild("Cód. Ficha:","width:80;height:10;background:#c0c0c0")
	set tabPDF(1,6)=$listbuild(codFicha,"width:48;height:10;")
	;
	set tabPDF(2,3)=$listbuild("Data Consulta:","width:100;height:10;background:#c0c0c0")
	set tabPDF(2,4)=$listbuild($zdate($piece(gcaemed8,Z,1),4),"width:80;height:10;")
	;
	set tabPDF(3,3)=$listbuild("Peso:","width:44;height:10;background:#c0c0c0")
	set tabPDF(3,4)=$listbuild($piece(gcaemed8,Z,3)_" Kg","width:80;height:10;")
	;
	set tabPDF(4,3)=$listbuild("Altura:","width:50;height:10;background:#c0c0c0")
	set tabPDF(4,4)=$listbuild($piece(gcaemed8,Z,2)_" cm","width:80;height:10;")
	;
	; # Patologia
	;
	set tabPDF(8,1)=$listbuild("Patologias","width:800;height:10;background:#c0c0c0")
	;
	set linha = 9
	set linha=$increment(linha)
	;
	set (codPatologia,desPatologia)=""
	for  set codPatologia=$order(^GCAEMED(codEmpresa,8,cpfCliente,codFicha,1,codPatologia)) quit:codPatologia=""  do
	. ;
	. set desPatologia=$piece($get(^GCAEMED(codEmpresa,3,codPatologia)),z,1)
	. ;
	. set tabPDF(linha,1)=$listbuild("-","width:15;height:10;align:right;")
	. set tabPDF(linha,2)=$listbuild(desPatologia,"width:800;height:10;")
	. ;
	. set linha=$increment(linha)
	;
	; # Medicamento
	;
	set linha=$increment(linha)
	set tabPDF(linha,1)=$listbuild("Meciamentos","width:800;height:10;background:#c0c0c0")
	;
	set linha=$increment(linha)
	set (codMedicamento,desMedicamento)=""
	for  set codMedicamento=$order(^GCAEMED(codEmpresa,8,cpfCliente,codFicha,2,codMedicamento)) quit:codMedicamento=""  do
	. ;
	. set desMedicamento=$piece($get(^GCAEMED(codEmpresa,4,codMedicamento)),z,1)
	. ;
	. set tabPDF(linha,1)=$listbuild("-","width:15;height:10;align:right;")
	. set tabPDF(linha,2)=$listbuild(desMedicamento,"width:800;height:10;")
	. ;
	. set linha=$increment(linha)
	;
	; # Refeição
	;
	set linha=$increment(linha)
	set tabPDF(linha,1)=$listbuild("Refeições","width:800;height:10;background:#c0c0c0")
	;
	set linha=$increment(linha)
	set (codRefeicao,desRefeicao)=""
	for  set codRefeicao=$order(^GCAEMED(codEmpresa,8,cpfCliente,codFicha,3,codRefeicao)) quit:codRefeicao=""  do
	. ;
	. set desRefeicao=$piece($get(^GCAEMED(codEmpresa,5,codRefeicao)),z,1)
	. ;
	. set tabPDF(linha,1)=$listbuild(desRefeicao,"width:400;height:10;border:1,1,1,1;")
	. ;
	. set linha=$increment(linha)
	. ;
	. ; # Alimentos dessa refeição
	. ;
	. set (codAlimentacao,desAlimentacao)=""
	. for  set codAlimentacao=$order(^GCAEMED(codEmpresa,8,cpfCliente,codFicha,3,codRefeicao,1,codAlimentacao)) quit:codAlimentacao=""  do 
	. . set desAlimentacao=$piece($get(^GCAEMED(codEmpresa,6,codAlimentacao)),z,1)
	. . ;
	. . set tabPDF(linha,1)=$listbuild("-","width:15;height:10;align:right;")
	. . set tabPDF(linha,2)=$listbuild(desAlimentacao,"width:800;height:10;")
	. . ;
	. . set linha=$increment(linha)
	;
	do Table^%CSW1PDFTab(.proxLinha,1,.tabPDF,"IniCab^GCAEMED600RG",,,10)
	set sc=$$finalizaPag^%CSW1PDF()
	;
	quit $$$OK
	;
	; Ini Cab PDF
	;
IniCab(proxLinha,numeroPagina)	;
	$$$VAR
	new linha,sc,tabPDF
	;
	set linha=0,proxLinha=583,numeroPagina=$increment(numeroPagina)
	;
	set sc=$$iniPagPDF^%CSW1PDF(842,595,,,,10)
	;
	kill tabPDF
	set tabPDF="cellpadding:1;font-family:Helvetica;font-size:12;borderlarg:0.4;border:0,0,0,0;"
	;
	; # Nova linha
	;
	set linha=$increment(linha)
	set tabPDF(linha,1,1,1)=$listbuild("","width:820;height:10;")
	;
	set linha=$increment(linha)
	;
	do Table^%CSW1PDFTab(.proxLinha,1,.tabPDF)
	;
	quit $$$OK
	;
	;
	; csw:csp:naogerar
	;
